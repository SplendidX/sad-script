// SAD-Script: S-Expressions and Dashes

// non-terminals
<program> ::= (<function> | <statement>)*
<function> ::= INTRINSIC? FUNCTION (IDENTIFIER | EQUAL) IDENTIFIER* <body>
<body> ::= OPEN_BRACE <statement>* CLOSE_BRACE
<expression> ::= INT_LIT | DOUBLE_LIT | BOOL_LIT | STRING_LIT | NIL | <call> | <infix-call> | IDENTIFIER | <query>
<call> ::= OPEN_PAREN IDENTIFIER IDENTIFIER* CLOSE_PAREN
<infix-call> ::= OPEN_BRACKET <expression> IDENTIFIER <expression> CLOSE_BRACKET
<query> ::= OPEN_PAREN PIPE <expression> <query-step>* PIPE CLOSE_PAREN
<query-step> ::= ARROW IDENTIFIER <expression>* <query-pred>?
<query-pred> ::= COLON IDENTIFIER <expression>
<statement> ::= 
   <call> | <infix-call> | <var> | <set> | <if> | <for> | <for-each> | <while> | <do> | <switch> | <return> | <die>
<var> ::= VAR IDENTIFIER EQUAL <expression>
<set> ::= SET IDENTIFIER EQUAL <expression>
<if> ::= IF <expression> <body> (ELSEIF <expression> <body>)* (ELSE <body>)?
<for> ::= FOR IDENTIFIER EQUAL <expression> TO <expression> <body>
<foreach> ::= FOREACH IDENTIFIER (AT IDENTIFIER)? IN <expression> <body>
<while> ::= WHILE <expression> <body>
<do> ::= DO <body> WHILE <expression>
<switch> ::= SWITCH <expression> OPEN_BRACE (CASE <expression> <body>)* (DEFAULT <body>)? CLOSE_BRACE
<return> ::= RETURN <expression>
<die> ::= DIE <expression>

// terminals
INT_LIT
DOUBLE_LIT
BOOL_LIT
STRING_LIT
OPEN_PAREN
CLOSE_PAREN
OPEN_BRACKET
CLOSE_BRACKET
OPEN_BRACE
CLOSE_BRACE
PIPE
COLON
EQUAL
IDENTIFIER
FUNCTION
VAR
SET
IF
ELSE
ELSEIF
FOR
TO
FOREACH
AT
IN
WHILE
DO
SWITCH
CASE
DEFAULT
RETURN
DIE
INTRINSIC
NIL
ARROW
